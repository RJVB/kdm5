
dnl this is for kdm:

savex="$exec_prefix"
test "x$exec_prefix" = xNONE && exec_prefix="$prefix"
KDE_CONFDIR=`eval echo $kde_confdir`
AC_DEFINE_UNQUOTED(KDE_CONFDIR, "$KDE_CONFDIR", [KDE's configuration directory])
AC_SUBST(KDE_CONFDIR)
KDE_BINDIR=`eval echo $kde_bindir`
AC_DEFINE_UNQUOTED(KDE_BINDIR, "$KDE_BINDIR", [KDE's binaries directory])
AC_SUBST(KDE_BINDIR)
exec_prefix="$savex"

KDE_FIND_PATH(X, X_SERVER)
if test -n "$X_SERVER"; then
    XBINDIR=`echo $X_SERVER | sed -e 's#/X$##'`
else
    KDE_FIND_PATH(xrdb, XRDB)
    if test -n "$XRDB"; then
        XBINDIR=`echo $XRDB | sed -e 's#/xrdb$##'`
    else
	AC_MSG_ERROR([X binaries not found. Please make sure they are in PATH!])
    fi
fi
AC_DEFINE_UNQUOTED(XBINDIR, "$XBINDIR", [Define where to find the X binaries])
AC_SUBST(XBINDIR)

KDE_FIND_PATH(xmkmf, XMKMF, [], [AC_MSG_ERROR([xmkmf/imake not found. Please make sure it's in PATH!])])
AC_SUBST(XMKMF)

dnl doesn't work currently
dnl AC_CHECK_FUNCS(getsecretkey getloadavg getusershell login_getclass auth_timeok)

dnl ask imake about various X settings
AC_MSG_CHECKING([various X settings])
AC_CACHE_VAL(kde_cv_defines_imake,
[rm -fr conftestdir
if mkdir conftestdir; then
    cd conftestdir
    cat > Imakefile <<'EOF'
BASIC_FLAGS = StandardDefines ConnectionFlags 

#if HasXdmAuth
XDMAUTH_DEFINES = -DHASXDMAUTH
#endif

#if HasBSD44Sockets
SOCK_DEFINES = -DBSD44SOCKETS
#endif

#if defined(i386Architecture) || defined(AmigaArchitecture)
FRAGILE_DEFINES = -DFRAGILE_DEV_MEM
#endif

#ifdef OpenBSDArchitecture
RANDOM_DEFINES = -DARC4_RANDOM
#elif defined(LinuxArchitecture)
RANDOM_DEFINES = -DDEV_RANDOM
#endif

#if HasSetUserContext
USER_CONTEXT_DEFINES = -DHAS_SETUSERCONTEXT
#endif

#if HasLibCrypt && !defined(SpecialLibCrypt) && defined(LynxOSArchitecture)
CRYPT_DEFINES = -DHAS_CRYPT
#endif

XDM_CFLAGS = $(BASIC_FLAGS) $(XDMAUTH_DEFINES) $(FRAGILE_DEFINES) $(SOCK_DEFINES) $(RANDOM_DEFINES) $(USER_CONTEXT_DEFINES) $(CRYPT_DEFINES)

acimake:
	@echo XDM_CFLAGS=\"$(XDM_CFLAGS)\"
#if HasXdmAuth
	@echo HASXDMAUTH=1
#endif
EOF
    $XMKMF >&5 2>&1
    if test -f Makefile; then
	[kde_cv_defines_imake=`${MAKE-make} acimake 2> /dev/null | grep '^[^ ]*='`]
	kde_cv_defines_imake=`echo $kde_cv_defines_imake`
    else
	AC_MSG_ERROR([$XMKF (imake) failed])
    fi
    cd ..
    rm -fr conftestdir
else
    AC_MSG_ERROR([mkdir failed])
fi])
if test -n "$kde_cv_defines_imake"; then
    eval $kde_cv_defines_imake
    AC_MSG_RESULT([done])
fi

AC_MSG_CHECKING([whether to use xdm authorization])
if test -n "$HASXDMAUTH"; then
    AC_MSG_RESULT([yes])
    AC_DEFINE(HASXDMAUTH, 1, [Use encrypted XDM authorization])
else
    AC_MSG_RESULT([no])
fi

AC_CHECK_LIB(util, main, [LIBUTIL="-lutil"]) dnl for FreeBSD
AC_SUBST(LIBUTIL)
AC_CHECK_LIB(s, main, [LIB_LIBS="-ls"]) dnl for AIX
AC_SUBST(LIB_LIBS)

AC_CHECK_LIB(Xau, main, [:], 
	[
	  AC_MSG_WARN([Cannot build KDM! Make sure that libXau.a is installed!])
	  DO_NOT_COMPILE="$DO_NOT_COMPILE kdm"
	], 
	  $X_LDFLAGS -lX11 $LIBSOCKET)

AC_ARG_WITH(xdmcp,
	[  --without-xdmcp         build kdm without xdmcp support [default=with xdmcp]],
	[ if test "$withval" = yes; then
	    XDMCP=1
	  else
	    XDMCP=
	  fi
	], [XDMCP=1])
if test -n "$XDMCP"; then
  kdmchooserdir=chooser
  AC_CHECK_LIB(Xdmcp, main, [LIBXDMCP="-lXdmcp"], , $X_LDFLAGS -lX11 $LIBSOCKET)
  if test -n "$LIBXDMCP"; then
    ac_cpp_safe=$ac_cpp
    ac_cpp='$CXXCPP $CPPFLAGS $X_INCLUDES'
    AC_CHECK_HEADER(X11/Xdmcp.h, [HAVE_X11_XDMCP_H=1])
    ac_cpp=$ac_cpp_safe
  fi
  if test -z "$HAVE_X11_XDMCP_H"; then
    AC_MSG_WARN([Cannot build KDM! Make sure that libXdmcp.a and Xdmcp.h 
are installed or use --without-xdmcp to disable XDMCP support!])
    DO_NOT_COMPILE="$DO_NOT_COMPILE kdm"
  fi
fi
AC_SUBST(LIBXDMCP)
AC_SUBST(kdmchooserdir)	dnl XXX phase out

AC_MSG_CHECKING(whether to use Kerberos v4)
AC_ARG_WITH(krb4,
[  --with-krb4[=PATH]      Compile in Kerberos v4 support.],
[ case "$withval" in
  yes)
    with_krb4=/usr/kerberos
    ;;
  esac ],
[ with_krb4=no ]
)
case "$with_krb4" in
no)
  AC_MSG_RESULT(no)
  ;;
*)
  AC_MSG_RESULT(yes)
  AC_DEFINE_UNQUOTED(KRB4, 1, [ define if you have Kerberos IV ])
  KERBEROS_ROOT="$with_krb4"
  KERBEROS_INCS="-I${KERBEROS_ROOT}/include"
  KERBEROS_LIBS="-L${KERBEROS_ROOT}/lib -lkrb -ldes"
  KRB_RPATH=
  if test "$USE_RPATH" = "yes" ; then
    KRB_RPATH="-R ${KERBEROS_ROOT}/lib"
  fi
  AC_CHECK_LIB(resolv, dn_expand, KERBEROS_LIBS="$KERBEROS_LIBS -lresolv")
  dnl Check whether or not the AFS lifetime conversion routines exist.
  AC_MSG_CHECKING(whether AFS lifetime conversion routines are present)
  keeplibs="$LIBS"
  keepcflags="$CFLAGS"
  LIBS="-L${KERBEROS_ROOT}/lib -lkrb -ldes $LIBS"
  CFLAGS="-I${KERBEROS_ROOT}/include $CFLAGS"
  AC_TRY_LINK([#include <krb.h>], [ krb_life_to_time(10, 10);],
              [AC_MSG_RESULT(yes)
               AC_DEFINE_UNQUOTED(HAVE_KRB_LIFE_TO_TIME, 1, 
	         [ define if libkrb has krb_life_to_time() ])],
              [AC_MSG_RESULT(no)])
  LIBS="$keeplibs"
  CFLAGS="$keepcflags"
  ;;
esac

AC_MSG_CHECKING(whether to use AFS)
AC_ARG_WITH(afs,
  [  --with-afs              Compile in AFS support (requires KTH krb4).], , 
  [ with_afs=no ])
if test "$with_afs" = no; then
  AC_MSG_RESULT(no)
else
  if test "$with_krb4" = no; then
    AC_MSG_RESULT(no)
    AC_MSG_WARN("AFS requires Kerberos v4 support.")
    with_afs=no
  else
    AC_MSG_RESULT(yes)
    AC_DEFINE_UNQUOTED(AFS, 1, [ define if you have KTH Kerberos IV and AFS ])
    KERBEROS_LIBS="${KERBEROS_LIBS} -lkafs"
    if test -n "$os_aix"; then
      KERBEROS_LIBS="${KERBEROS_LIBS} -lld"
    fi
  fi
fi

AC_SUBST(KERBEROS_ROOT)dnl
AC_SUBST(KERBEROS_INCS)dnl
AC_SUBST(KERBEROS_LIBS)dnl
AC_SUBST(KRB_RPATH)

AC_MSG_CHECKING([which CFLAGS to use for xdm core])
if eval "test \"`echo '$ac_cv_func_vsyslog'`\" = yes"; then
    XDM_CFLAGS="$XDM_CFLAGS -DUSE_SYSLOG"
fi
if test "x$use_pam" = xyes; then
    XDM_CFLAGS="$XDM_CFLAGS -DUSE_PAM"
elif test "x$use_shadow" = xyes; then
    XDM_CFLAGS="$XDM_CFLAGS -DUSESHADOW"
fi
if test "x$XDMCP" != x; then
    XDM_CFLAGS="$XDM_CFLAGS -DXDMCP"
fi
if test "x$with_krb4" != xno; then
    XDM_CFLAGS="$XDM_CFLAGS -DKERBEROS"
    if test "x$with_afs" = xno; then
	XDM_CFLAGS="$XDM_CFLAGS -DNO_AFS"
    fi
fi
if eval "test \"`echo '$ac_cv_header_krb5_krb5_h'`\" = yes"; then
    XDM_CFLAGS="$XDM_CFLAGS -DK5AUTH"
fi
if eval "test \"`echo '$ac_cv_header_rpc_rpc_h'`\" = yes"; then
    XDM_CFLAGS="$XDM_CFLAGS -DSECURE_RPC"
fi
AC_MSG_RESULT([$XDM_CFLAGS])
AC_SUBST(XDM_CFLAGS)

dnl AC_OUTPUT(kdm/kfrontend/config/kdmrc)
dnl AC_OUTPUT(kdm/kfrontend/config/Xservers)
dnl AC_OUTPUT(kdm/kfrontend/config/Xsetup)
dnl AC_OUTPUT(kdm/kfrontend/config/Xstartup)
dnl AC_OUTPUT(kdm/kfrontend/config/Xreset)
